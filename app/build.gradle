apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: "com.vanniktech.android.junit.jacoco"

android {
    compileSdkVersion 27
    buildToolsVersion '28.0.2'
    defaultConfig {
        applicationId "com.noprestige.kanaquiz"
        minSdkVersion 17
        targetSdkVersion 27
        versionCode 14
        versionName "0.12"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    buildTypes {
        release {
            testCoverageEnabled true
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
            applicationIdSuffix ".dev"
            versionNameSuffix " (dev)"
            debuggable true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'com.android.support:appcompat-v7:27.1.1'
    testImplementation 'junit:junit:4.12'
    implementation 'com.android.support:design:27.1.1'

    implementation "android.arch.persistence.room:runtime:1.1.1"
    annotationProcessor "android.arch.persistence.room:compiler:1.1.1"

    androidTestImplementation "android.arch.persistence.room:testing:1.1.1"

    implementation('com.crashlytics.sdk.android:crashlytics:2.9.5@aar') {
        transitive = true
    }
    implementation 'com.google.firebase:firebase-core:16.0.3'
    implementation 'com.google.firebase:firebase-perf:16.1.0'

    implementation 'org.apmem.tools:layouts:1.10@aar'

    implementation 'joda-time:joda-time:2.10'
    //implementation 'org.joda:joda-convert:2.1.1' //fixes issues with proguard
    //using 'classic' variant fixes issues with desugar
    //ref: https://github.com/JodaOrg/joda-convert/issues/24
    implementation group: 'org.joda', name: 'joda-convert', version: '2.1.1', classifier: 'classic'
}

repositories {
    maven {
        url 'https://maven.google.com'
        // Alternative URL is 'https://dl.google.com/dl/android/maven2/'
    }
}

coveralls {
    sourceDirs += "${rootDir}/app/src/main/java/"
    //Unit tests
    jacocoReportPath += "${buildDir}/reports/jacoco/debug/jacoco.xml"
    //Instrument tests
    jacocoReportPath += "${buildDir}/reports/coverage/debug/report.xml"
}

apply plugin: 'com.google.gms.google-services'
